buildscript {

    ext.isTravis = System.getenv("TRAVIS") == "true"
    ext.travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    ext.isReleaseVersion = !isTravis

    ext.kotlin_version = '1.3.41'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "com.sksamuel.hoplite"

    if (isTravis) {
        version "1.1.0." + travisBuildNumber + "-SNAPSHOT"
    } else {
        version "1.0.1"
    }

    task javadocJar(type: Jar) {
        from javadoc.destinationDir
        archiveClassifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
       // from sourceSets.main.kotlin
        archiveClassifier = 'sources'
    }

    tasks.withType(Javadoc) {
        onlyIf { isReleaseVersion }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile "io.kotlintest:kotlintest-runner-junit5:3.4.0"
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    publishing {
        repositories {
            maven {

                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

                name 'deploy'

                url = isTravis ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = System.getenv("OSSRH_USERNAME") ?: ossrhUsername
                    password = System.getenv("OSSRH_PASSWORD") ?: ossrhPassword
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {

                from components.java

                if (isReleaseVersion) {
                    artifact sourcesJar
                    artifact javadocJar
                }

                pom {
                    name = 'hoplite'
                    description = 'Configuration for Kotlin'
                    url = 'http://www.github.com/sksamuel/hoplite'

                    scm {
                        connection = 'scm:git:http://www.github.com/sksamuel/hoplite'
                        developerConnection = 'scm:git:http://github.com/sksamuel'
                        url = 'http://www.github.com/sksamuel/hoplite'
                    }

                    licenses {
                        license {
                            name = 'The Apache 2.0 License'
                            url = 'https://opensource.org/licenses/Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'sksamuel'
                            name = 'Stephen Samuel'
                            email = 'sam@sksamuel.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }

}

project(":") {
    publish.enabled = false
}

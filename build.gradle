buildscript {

    ext.isTravis = System.getenv("TRAVIS") == "true"
    ext.travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
    ext.isReleaseVersion = !isTravis

    ext.kotlin_version = '1.3.50'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "com.sksamuel.hoplite"

    if (isTravis) {
        version "1.1.0." + travisBuildNumber + "-SNAPSHOT"
    } else {
        version "1.0.8"
    }

    tasks.withType(Javadoc) {
        onlyIf { isReleaseVersion }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation 'io.arrow-kt:arrow-core-data:0.10.0'
        implementation 'io.arrow-kt:arrow-validation:0.10.0'
        testImplementation 'io.kotlintest:kotlintest-assertions:3.4.2'
        testImplementation 'io.kotlintest:kotlintest-assertions-arrow:3.4.2'
        testImplementation "io.kotlintest:kotlintest-runner-junit5:3.4.2"
    }

    test {
        useJUnitPlatform()
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}
project(":") {
    publish.enabled = false
}
